10 REM DEAD END - Wolfgang Thaller for Macintosh (C) 1993-1998
20 REM Miguel A. Garcia Prada for ZX-Spectrum 2019
30 REM Concurso Basic Bytemaniacos 2019-2020

35 BORDER 1: PAPER 0: INK 0
40 CLEAR 59999
50 LET BUFFER=60000
60 LET NIVEL=2

500 REM LANZANDO EL NIVEL
510 LET MOV=0
515 PRINT #0; AT 0,0; PAPER 1; INK 5; "{-3}{-3}{-3}{-3}{-3}{-3}{-3}{-3}{-3}{-3}{-3}{-3}{-3}{-3}{-3}{-3}{-3}{-3}{-3}{-3}{-3}{-3}{-3}{-3}{-3}{-3}{-3}{-3}{-3}{-3}{-3}{-3}"
520 PRINT #0; AT 1,3; PAPER 1; INK 5; "Nivel: ";NIVEL+1; TAB 15; "Movimientos:     "; AT 1,28; MOV
522 GO SUB 800
525 GO SUB 1000
530 PRINT AT YPROTA, XPROTA; INK 3; "{I}{J}"; AT YPROTA+1, XPROTA;"{K}{L}"
535 LET PUNT=BUFFER+(((YPROTA/2)*16)+(XPROTA/2))
540 GO TO 1100

800 REM BARRIDO CLS
805 FOR F=21 TO 0 STEP -1
810 PRINT AT F,0; INK 0; PAPER 0; "                                "
815 NEXT F
820 RETURN

900 REM FINAL DEL NIVEL
902 GO SUB 800
905 PRINT FLASH 1; INK 2; PAPER 6; AT 10,10;"FINAL"
910 STOP

1000 REM MAPEANDO
1005 LET XTILE=0: LET YTILE=0
1010 RESTORE 9000+(NIVEL*20)
1015 FOR F=BUFFER TO BUFFER+175: READ A: POKE F,A
1020 IF A=0 THEN GO TO 1050
1025 IF A=1 THEN PRINT AT YTILE, XTILE; INK 2; PAPER 0; "{A}{B}"; AT YTILE+1,XTILE; "{C}{D}": GO TO 1050
1025 IF A=2 THEN PRINT AT YTILE, XTILE; INK 1; PAPER 0; "{E}{F}"; AT YTILE+1,XTILE; "{G}{H}": GO TO 1050
1030 IF A=3 THEN PRINT AT YTILE, XTILE; INK 6; PAPER 0; "{+4}{-7}"; AT YTILE+1,XTILE; "{+1}{+2}": GO TO 1050
1050 LET XTILE=XTILE+2
1055 IF XTILE>=32 THEN LET XTILE=0: LET YTILE=YTILE+2
1060 NEXT F
1065 READ XPROTA: READ YPROTA
1070 RETURN

1100 REM BUCLE PRINCIPAL
1105 LET T$=INKEY$
1210 IF T$="O" OR T$="o" THEN GO TO 1300
1215 IF T$="P" OR T$="p" THEN GO TO 1400
1220 IF T$="Q" OR T$="q" THEN GO TO 1500
1225 IF T$="A" OR T$="a" THEN GO TO 1600
1230 IF T$="R" OR T$="r" THEN GO TO 500
1245 GO TO 1105

1300 REM MOVIMIENTO IZQUIERDA
1305 LET PUNT=PUNT-1: LET B1=PEEK (PUNT): LET PUNT=PUNT-1: LET B2=PEEK (PUNT): LET PUNT=PUNT+3: LET B3=PEEK (PUNT): LET PUNT=PUNT-1
1310 IF B1<>0 THEN GO TO 1320
1315 GO SUB 2000: GO SUB 2000: LET PUNT=PUNT-1: GO TO 1895
1320 IF B1=2 AND B2=0 AND B3=0 THEN GO TO 1350
1325 IF B1=3 THEN GO SUB 2000: GO SUB 2000: GO TO 900
1330 GO TO 1900
1350 REM EMPUJAMOS A LA IZQUIERDA
1355 LET PUNT=PUNT-1: POKE PUNT,0: POKE PUNT-1,2
1357 GO SUB 2050: GO SUB 2050: GO SUB 2000: GO SUB 2000
1360 LET XPROTA=XPROTA-1
1365 PRINT AT YPROTA, XPROTA-2;INK 1;"{E}{F}";INK 3;"{I}{J} ";AT YPROTA+1, XPROTA-2;INK 1;"{G}{H}";INK 3;"{K}{L} "
1370 LET XPROTA=XPROTA-1
1375 PRINT AT YPROTA, XPROTA-2;INK 1;"{E}{F}";INK 3;"{I}{J} ";AT YPROTA+1, XPROTA-2;INK 1;"{G}{H}";INK 3;"{K}{L} "
1380 GO TO 1895

1400 REM MOVIMIENTO DERECHA
1405 LET PUNT=PUNT+1: LET B1=PEEK (PUNT): LET PUNT=PUNT+1: LET B2=PEEK (PUNT): LET PUNT=PUNT-3: LET B3=PEEK (PUNT): LET PUNT=PUNT+1
1410 IF B1<>0 THEN GO TO 1420
1415 GO SUB 2050: GO SUB 2050: LET PUNT=PUNT+1: GO TO 1895
1420 IF B1=2 AND B2=0 AND B3=0 THEN GO TO 1450
1425 IF B1=3 THEN GO SUB 2050: GO SUB 2050: GO TO 900
1430 GO TO 1900
1450 REM EMPUJAMOS A LA DERECHA
1455 LET PUNT=PUNT+1: POKE PUNT,0: POKE PUNT+1,2
1457 GO SUB 2000: GO SUB 2000: GO SUB 2050: GO SUB 2050
1460 LET XPROTA=XPROTA+1
1465 PRINT AT YPROTA, XPROTA-1;INK 3;" {I}{J}";INK 1;"{E}{F}";AT YPROTA+1, XPROTA-1;INK 3;" {K}{L}";INK 1;"{G}{H}"
1470 LET XPROTA=XPROTA+1
1475 PRINT AT YPROTA, XPROTA-1;INK 3;" {I}{J}";INK 1;"{E}{F}";AT YPROTA+1, XPROTA-1;INK 3;" {K}{L}";INK 1;"{G}{H}"
1480 GO TO 1895

1500 REM MOVIMIENTO ARRIBA
1505 LET PUNT=PUNT-16: LET B1=PEEK (PUNT): LET PUNT=PUNT-16: LET B2=PEEK (PUNT): LET PUNT=PUNT+48: LET B3=PEEK (PUNT): LET PUNT=PUNT-16
1510 IF B1<>0 THEN GO TO 1520
1515 GO SUB 2075: GO SUB 2075: LET PUNT=PUNT-16: GO TO 1895
1520 IF B1=2 AND B2=0 AND B3=0 THEN GO TO 1550
1525 IF B1=3 THEN GO SUB 2075: GO SUB 2075: GO TO 900
1530 GO TO 1900
1550 REM EMPUJAMOS ARRIBA
1555 LET PUNT=PUNT-16: POKE PUNT,0: POKE PUNT-16,2
1557 GO SUB 3025: GO SUB 3025: GO SUB 2075: GO SUB 2075
1565 PRINT AT YPROTA-3, XPROTA;INK 1;"{E}{F}"; AT YPROTA-2,XPROTA; "{G}{H}": GO SUB 2075
1575 PRINT AT YPROTA-3, XPROTA;INK 1;"{E}{F}"; AT YPROTA-2,XPROTA; "{G}{H}": GO SUB 2075
1580 GO TO 1895

1600 REM MOVIMIENTO ABAJO
1605 LET PUNT=PUNT+16: LET B1=PEEK (PUNT): LET PUNT=PUNT+16: LET B2=PEEK (PUNT): LET PUNT=PUNT-48: LET B3=PEEK (PUNT): LET PUNT=PUNT+16
1610 IF B1<>0 THEN GO TO 1620
1615 GO SUB 3025: GO SUB 3025: LET PUNT=PUNT+16: GO TO 1895
1620 IF B1=2 AND B2=0 AND B3=0 THEN GO TO 1650
1625 IF B1=3 THEN GO SUB 3025: GO SUB 3025: GO TO 900
1630 GO TO 1900
1650 REM EMPUJAMOS ABAJO
1655 LET PUNT=PUNT+16: POKE PUNT,0: POKE PUNT+16,2
1657 GO SUB 2075: GO SUB 2075: GO SUB 3025: GO SUB 3025
1665 PRINT AT YPROTA+3, XPROTA;INK 1;"{E}{F}"; AT YPROTA+4,XPROTA; "{G}{H}": GO SUB 3025
1675 PRINT AT YPROTA+3, XPROTA;INK 1;"{E}{F}"; AT YPROTA+4,XPROTA; "{G}{H}": GO SUB 3025
1680 GO TO 1895

1895 LET MOV=MOV+1
1900 PRINT #0; PAPER 1; INK 5; AT 1,28; MOV
1905 GO TO 1100

2000 REM PINTA PERSONAJE UN PASO A LA IZQUIERDA BORRANDO RASTRO
2005 LET XPROTA=XPROTA-1
2010 PRINT AT YPROTA, XPROTA; INK 3; "{I}{J} "; AT YPROTA+1, XPROTA;"{K}{L} "
2012 BEEP 0.1,-60
2015 RETURN

2050 REM PINTA PERSONAJE UN PASO A LA DERECHA BORRANDO RASTRO
2055 PRINT AT YPROTA, XPROTA; INK 3; " {I}{J}"; AT YPROTA+1, XPROTA;" {K}{L}"
2060 LET XPROTA=XPROTA+1
2062 BEEP 0.1,-60
2065 RETURN

2075 REM PINTA PERSONAJE UN PASO ARRIBA BORRANDO RASTRO
2080 LET YPROTA=YPROTA-1
2085 PRINT AT YPROTA, XPROTA; INK 3; "{I}{J}"; AT YPROTA+1, XPROTA;"{K}{L}"; AT YPROTA+2, XPROTA;"  "
2087 BEEP 0.1,-60
2090 RETURN

3025 REM PINTA PERSONAJE UN PASO ABAJO BORRANDO RASTRO
3030 PRINT AT YPROTA,XPROTA;"  "
3035 LET YPROTA=YPROTA+1
3040 PRINT AT YPROTA, XPROTA; INK 3; "{I}{J}"; AT YPROTA+1, XPROTA;"{K}{L}"
3042 BEEP 0.1,-60
3045 RETURN

3100 REM PINTA PERSONAJE UN PASO A LA IZQUIERDA NO BORRANDO RASTRO
3105 LET XPROTA=XPROTA-1
3110 PRINT AT YPROTA, XPROTA; INK 3; "{I}{J}"; AT YPROTA+1, XPROTA;"{K}{L}"
3112 BEEP 0.1,-60
3115 RETURN

3120 REM PINTA PERSONAJE UN PASO A LA DERECHA NO BORRANDO RASTRO
3125 LET XPROTA=XPROTA+1
3130 PRINT AT YPROTA, XPROTA; INK 3; "{I}{J}"; AT YPROTA+1, XPROTA;"{K}{L}"
3135 RETURN

9000 REM NIVEL1
9001 DATA 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
9002 DATA 0,0,0,0,0,0,1,1,1,0,0,0,0,0,0,0
9003 DATA 0,0,0,1,1,1,1,3,1,1,1,1,0,0,0,0
9004 DATA 0,1,1,1,0,2,0,2,0,0,0,1,1,1,0,0
9005 DATA 1,1,0,0,0,0,2,2,2,0,2,0,0,1,1,0
9006 DATA 1,2,0,2,0,2,0,2,0,0,0,2,0,2,1,0
9007 DATA 1,2,0,0,2,0,2,2,2,2,2,0,0,2,1,0
9008 DATA 1,2,2,2,0,2,0,0,2,0,0,2,2,2,1,0
9009 DATA 1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,0
9010 DATA 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
9011 DATA 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
9012 DATA 14,14: REM COORDENADAS DEL PROTA

9020 REM NIVEL 2
9021 DATA 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
9022 DATA 1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,0
9023 DATA 1,0,0,2,0,2,0,0,0,2,0,2,0,3,1,0
9024 DATA 1,0,0,2,0,2,0,2,2,2,2,0,2,0,1,0
9025 DATA 1,0,0,2,0,2,0,0,0,2,0,2,0,0,1,0
9026 DATA 1,0,0,2,0,2,0,2,2,0,2,0,2,0,1,0
9027 DATA 1,0,0,2,0,2,0,0,0,2,0,2,0,2,1,0
9028 DATA 1,0,0,2,0,2,0,2,2,0,2,0,2,0,1,0
9029 DATA 1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,0
9030 DATA 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
9031 DATA 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
9032 DATA 2,8: REM COORDENADAS DEL PROTA


9040 REM NIVEL 3
9041 DATA 0,1,1,1,1,1,1,1,1,1,1,1,1,1,1,0
9042 DATA 0,1,0,0,0,2,2,0,0,0,0,0,2,0,1,0
9043 DATA 0,1,0,0,0,0,0,0,2,2,0,2,0,2,1,0
9044 DATA 0,1,0,0,0,2,2,0,2,0,2,0,2,0,1,0
9045 DATA 0,1,0,0,2,2,0,0,2,2,2,2,2,0,1,0
9046 DATA 0,1,0,0,2,0,2,0,2,0,0,0,0,0,1,0
9047 DATA 0,1,0,0,0,2,0,2,0,0,2,0,0,2,1,0
9048 DATA 0,1,0,0,0,0,2,0,2,0,2,2,2,2,1,0
9049 DATA 0,1,0,0,0,2,2,0,0,2,2,2,3,0,1,0
9050 DATA 0,1,0,0,0,2,2,0,0,2,0,0,0,0,1,0
9051 DATA 0,1,1,1,1,1,1,1,1,1,1,1,1,1,1,0
9052 DATA 4,10: REM COORDENADAS DEL PROTA

9060 REM NIVEL 4

9080 REM NIVEL 5

9100 REM NIVEL 6

9120 REM NIVEL 7

9140 REM NIVEL 8

9160 REM NIVEL 9

9180 REM NIVEL 10